dnl
dnl configure.in for qalculate
dnl

dnl  ----------------------
dnl | initialize autotools |---------------------------------------------------
dnl  ----------------------

AC_INIT(configure.in)
AM_INIT_AUTOMAKE(qalculate-gtk, 0.4)
AM_CONFIG_HEADER(config.h)
AM_MAINTAINER_MODE

dnl  -------------------------------
dnl | check for neccessary programs |------------------------------------------
dnl  -------------------------------

AC_ISC_POSIX
AC_PROG_CC
AC_PROG_CXX
AM_PROG_CC_STDC
AC_HEADER_STDC
AC_PROG_LIBTOOL
AC_PROG_INTLTOOL

dnl  ------------------------------------
dnl | check for compiler characteristics |-------------------------------------
dnl  ------------------------------------

dnl Use -Wall if we have gcc.
changequote(,)dnl
if test "x$GCC" = "xyes"; then
  case " $CFLAGS " in
    *[\ \	]-Wall[\ \	]*) ;;
    *) CFLAGS="$CFLAGS -Wall" ;;
  esac
fi
changequote([,])dnl

dnl  --------------------------------
dnl | check for neccessary libraries |-----------------------------------------
dnl  --------------------------------

AC_CHECK_LIB(pthread, pthread_create, [LIBS="$LIBS -lpthread"])

PKG_CHECK_MODULES(PACKAGE, [
	gtk+-2.0 >= 1.3.13
	libglade-2.0
	libxml-2.0
	])
AC_SUBST(PACKAGE_CFLAGS)
AC_SUBST(PACKAGE_LIBS)

AC_LANG_SAVE
AC_LANG_CPLUSPLUS
AC_MSG_CHECKING([for strptime])
AC_TRY_COMPILE([#include <time.h>],[
   tm newTime;
   strptime("12-04-2000", "%A %h %d %H:%M:%S %Y", &newTime);],
have_strptime_prototype=yes, have_strptime_prototype=no)
AC_MSG_RESULT([$have_strptime_prototype])
if test $have_strptime_prototype = yes; then
	AC_DEFINE(HAVE_STRPTIME_PROTOTYPE,1,[If strptime is directly available.])
fi
AC_LANG_RESTORE

dnl  --------------------------------
dnl | check for GMP and CLN         |-----------------------------------------
dnl  --------------------------------

dnl AC_CHECK_LIB(gmp, __gmpz_init, , [AC_MSG_ERROR([GNU MP not found, see http://swox.com/gmp])])

AC_LANG([C++])


AC_PATH_CLN(1.1.0, [
     	LIBS="$LIBS $CLN_LIBS"
     	CPPFLAGS="$CPPFLAGS $CLN_CPPFLAGS"
  	AC_DEFINE(HAVE_LIBCLN,1,[If CLN can be used])
], AC_MSG_ERROR([No suitable installed version of CLN could be found.]))

AC_CHECK_LIB(giac, main, [
	LIBS="$LIBS -lgiac"
	AC_DEFINE(HAVE_GIAC,1,[If giac can be used])
], AC_MSG_WARN([No suitable installed version of giac could be found.]))

dnl  -------------------------------------
dnl | internationalization (i18n) support |------------------------------------
dnl  -------------------------------------

GETTEXT_PACKAGE=qalculate-gtk
AC_SUBST(GETTEXT_PACKAGE)
AC_DEFINE_UNQUOTED(GETTEXT_PACKAGE,"$GETTEXT_PACKAGE",[The package name for gettext])

dnl Please keep this in alphabetical order
ALL_LINGUAS="sv"
AM_GLIB_GNU_GETTEXT

dnl AM_GLIB_GNU_GETTEXT sets $DATADIRNAME
AC_MSG_CHECKING(locale directory)
if test "x$prefix" = "xNONE"; then
  PACKAGE_LOCALE_DIR=$ac_default_prefix/share/locale
else
  PACKAGE_LOCALE_DIR=$prefix/share/locale
fi

AC_DEFINE_UNQUOTED(PACKAGE_LOCALE_DIR, "$PACKAGE_LOCALE_DIR",
	[The directory in which qalculate's locale data will be stored])
	 AC_MSG_RESULT("$PACKAGE_LOCALE_DIR")

dnl  --------
dnl | output |-----------------------------------------------------------------
dnl  --------

AC_OUTPUT([
  Makefile
  data/Makefile
  src/Makefile
  src/calclib/Makefile
  src/calclib/data/Makefile  
  po/Makefile.in
])

